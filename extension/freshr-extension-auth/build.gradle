plugins {
    id 'java'
    id 'java-library'
    id 'org.springframework.boot' version '3.1.3'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'maven-publish'
}

group = 'run.freshr'
version = '1.0.0.RELEASE'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven {
        url "https://:${GITHUB_READ_TOKEN}@maven.pkg.github.com/${GITHUB_OWNER}/*"
    }
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // Validation
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    // Configuration Processor
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // LOMBOK
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Redis
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'

    // Elasticsearch
    implementation 'org.springframework.boot:spring-boot-starter-data-elasticsearch'

    // JPA
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation "com.querydsl:querydsl-jpa:${dependencyManagement.importedProperties['querydsl.version']}:jakarta" // QueryDsl Jpa
    // QueryDsl Apt
    annotationProcessor "com.querydsl:querydsl-apt:${dependencyManagement.importedProperties['querydsl.version']}:jakarta"
    testImplementation 'com.querydsl:querydsl-jpa' // QueryDsl Jpa
    testAnnotationProcessor 'com.querydsl:querydsl-apt' // QueryDsl Apt

    // Jakarta
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    annotationProcessor 'jakarta.persistence:jakarta.persistence-api'

    // FreshR
    implementation "${group}:library-core-utils:1.0.0.RELEASE"
    implementation "${group}:library-core-enumeration-mapper:1.0.0.RELEASE"
    implementation "${group}:library-core-unit:1.0.0.RELEASE"
    implementation "${group}:library-core-exceptions:1.0.0.RELEASE"

    annotationProcessor "${group}:library-docs-search-docs:1.0.0.RELEASE"
    annotationProcessor 'org.springframework.boot:spring-boot-starter-web'
    annotationProcessor 'com.squareup:javapoet:1.13.0'

    api "${group}:configure-extensions:1.0.0.RELEASE"
    api "${group}:configure-jpa:1.0.0.RELEASE"
    api "${group}:configure-redis:1.0.0.RELEASE"
    api "${group}:configure-elasticsearch:1.0.0.RELEASE"

    api "${group}:freshr-extension-entity:1.0.0.RELEASE"
}

test {
    useJUnitPlatform()
}

bootJar {
    enabled = false
}

jar {
    enabled = true
    archiveClassifier = ''
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact("build/libs/$rootProject.name-$version" + ".jar") {
                extension 'jar'
            }
        }
    }

    repositories {
        maven {
            url = "${GITHUB_MAVEN_PACKAGE}/${GITHUB_OWNER}/${rootProject.name}"
            credentials {
                username System.getenv("GITHUB_ACTOR")
                password System.getenv("GITHUB_TOKEN")
            }
        }
    }

    publishMavenPublicationToMavenRepository.dependsOn(jar)
}
